plugins {
    id 'java'
    id 'maven-publish'
}

ext.buildType = System.getenv('BUILD_TYPE')
if (ext.buildType == null) {
    ext.buildType = 'debug'
}

ext.buildNumber = System.getenv('BUILD_NUMBER')
if (ext.buildNumber == null) {
    ext.buildNumber = '9999'
}

ext.buildMavenServerUrl = System.getenv('BUILD_MAVEN_SERVER_URL')
if (ext.buildMavenServerUrl == null) {
    ext.buildMavenServerUrl = 'NOTSET'
}

ext.buildMavenCredentialUsername = System.getenv('BUILD_MAVEN_CREDENTIAL_USERNAME')
if (ext.buildMavenCredentialUsername == null) {
    ext.buildMavenCredentialUsername = 'NOTSET'
}

ext.buildMavenCredentialPassword = System.getenv('BUILD_MAVEN_CREDENTIAL_PASSWORD')
if (ext.buildMavenCredentialPassword == null) {
    ext.buildMavenCredentialPassword = 'NOTSET'
}

ext.revision = '-SNAPSHOT'
if ('release'.equals(ext.buildType)) {
    ext.revision = '.' + ext.buildNumber
}

group 'com.htc.vita'
version '0.1' + ext.revision

project.buildDir = 'temp'

sourceSets {
    main {
        java.srcDirs = ['source/main/java']
        resources.srcDirs = ['source/main/resources']
    }

    test {
        java.srcDirs = ['source/test/java']
        resources.srcDirs = ['source/test/resources']
    }
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

publish {
    dependsOn assemble
}

// from plugin 'maven-publish'
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'core'
            from components.java

            pom {
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/ViveportSoftware/vita_core_java/blob/master/LICENSE.md'
                    }
                }
                scm {
                    url = 'https://github.com/ViveportSoftware/vita_core_java'
                }
            }
        }
    }
    repositories {
        if (!'NOTSET'.equals(project.ext.buildMavenServerUrl)
                && !'NOTSET'.equals(project.ext.buildMavenCredentialUsername)
                && !'NOTSET'.equals(project.ext.buildMavenCredentialPassword)) {
            maven {
                credentials {
                    username project.ext.buildMavenCredentialUsername
                    password project.ext.buildMavenCredentialPassword
                }
                url project.ext.buildMavenServerUrl
            }
        }
    }
}

task clearJar(type: Delete) {
    delete 'dist/jar'
}

task copyJar(type: Copy) {
    from configurations.runtime.artifacts.files
    into('dist/jar')
}

task dist(type: Copy, dependsOn: [build, clearJar, copyJar])
