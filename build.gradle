plugins {
    id 'java'
}

ext.buildType = System.getenv('BUILD_TYPE')
if (ext.buildType == null) {
    ext.buildType = 'debug'
}

ext.buildNumber = System.getenv('BUILD_NUMBER')
if (ext.buildNumber == null) {
    ext.buildNumber = '9999'
}

ext.revision = '-SNAPSHOT'
if ('release'.equals(ext.buildType)) {
    ext.revision = '.' + ext.buildNumber
}

group 'com.htc.vita'
version '0.1' + ext.revision

project.buildDir = 'temp'

sourceSets {
    main {
        java.srcDirs = ['source/main/java']
        resources.srcDirs = ['source/main/resources']
    }

    test {
        java.srcDirs = ['source/test/java']
        resources.srcDirs = ['source/test/resources']
    }
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task clearJar(type: Delete) {
    delete 'dist/jar'
}

task copyJar(type: Copy) {
    from configurations.runtime.artifacts.files
    into('dist/jar')
}

task dist(type: Copy, dependsOn: [build, clearJar, copyJar])
